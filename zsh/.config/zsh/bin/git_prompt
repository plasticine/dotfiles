#!/usr/bin/env zsh

set -euo pipefail

in_repo() {
	[[ $(git rev-parse --is-inside-work-tree) == true ]]
}

branch() {
	(git symbolic-ref -q HEAD || git name-rev --name-only --no-undefined --always HEAD) 2> /dev/null
}

branch_raw() {
	echo "${$(branch)#(refs/heads/|tags/)}" 2> /dev/null
}

commit() {
	git rev-parse --short HEAD 2> /dev/null || echo "NONE"
}

remote_commits_ahead() {
	local commits_ahead; commits_ahead="$(git log --oneline @{u}.. 2> /dev/null | wc -l | tr -d ' ')"
	echo -ne "${commits_ahead:-0}A"
}

remote_commits_behind() {
	local commits_behind; commits_behind="$(git log --oneline ..@{u} 2> /dev/null | wc -l | tr -d ' ')"
	echo -ne "${commits_behind:-0}B"
}

diff() {
	git log --oneline @{u}.. &> /dev/null

	if [[ x$? == x0 ]]; then
		local IFS=" "
		local diff; diff="$(git diff --cached --shortstat origin/master)"
		local changed; changed="${$(echo "$diff" | awk '{print $1}'):-0}"
		local inserted; inserted="${$(echo "$diff" | awk '{print $4}'):-0}"
		local deleted; deleted="${$(echo "$diff" | awk '{print $6}'):-0}"

		[[ $changed != 0 ]] && changed="%F{blue}∆${changed}%f" || changed="∆${changed}"
		[[ $inserted != 0 ]] && inserted="%F{green}+${inserted}%f" || inserted="+${inserted}"
		[[ $deleted != 0 ]] && deleted="%F{red}-${deleted}%f" || deleted="-${deleted}"

		declare -a stats

		stats+=( $changed )
		stats+=( $inserted )
		stats+=( $deleted )

		# echo " %F{blue}${changed:-0}%f %F{green}${inserted:-0}%f %F{red}${deleted:-0}%f"

		echo " $stats"
	fi
}

main() {
	if in_repo; then
		autoload -U colors && colors

		echo -ne "%F{white}$(branch_raw)@$(commit) $(remote_commits_ahead):$(remote_commits_behind)$(diff)%f%k"
	fi
}

main
