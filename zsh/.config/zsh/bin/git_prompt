#!/usr/bin/env zsh

set -euo pipefail

jj_repo() {
  jj root --quiet &> /dev/null
}

in_repo() {
	[[ $(git rev-parse --is-inside-work-tree) == true ]]
}

branch() {
	(git symbolic-ref -q HEAD || git name-rev --name-only --no-undefined --always HEAD) 2> /dev/null
}

branch_raw() {
	echo "${$(branch)#(refs/heads/|tags/)}" 2> /dev/null
}

commit() {
	git rev-parse --short HEAD 2> /dev/null || echo "NONE"
}

remote_commits_ahead() {
	local commits_ahead; commits_ahead="$(git log --oneline @{u}.. 2> /dev/null | wc -l | tr -d ' ')"
	echo -ne "${commits_ahead:-0}A"
}

remote_commits_behind() {
	local commits_behind; commits_behind="$(git log --oneline ..@{u} 2> /dev/null | wc -l | tr -d ' ')"
	echo -ne "${commits_behind:-0}B"
}

diff() {
	git log --oneline @{u}.. &> /dev/null

	if [[ x$? == x0 ]]; then
		local IFS=" "
		local diff; diff="$(git diff --cached --shortstat origin/master)"
		local changed; changed="${$(echo "$diff" | awk '{print $1}'):-0}"
		local inserted; inserted="${$(echo "$diff" | awk '{print $4}'):-0}"
		local deleted; deleted="${$(echo "$diff" | awk '{print $6}'):-0}"

		[[ $changed != 0 ]] && changed="%{%F{33}%}∆${changed}%f" || changed="∆${changed}"
		[[ $inserted != 0 ]] && inserted="%{%F{77}%}+${inserted}%f" || inserted="+${inserted}"
		[[ $deleted != 0 ]] && deleted="%{%F{202}%}-${deleted}%f" || deleted="-${deleted}"

		declare -a stats

		stats+=( $changed )
		stats+=( $inserted )
		stats+=( $deleted )

		# echo " %F{blue}${changed:-0}%f %F{green}${inserted:-0}%f %F{red}${deleted:-0}%f"

		echo " $stats"
	fi
}

jj_status() {
  jj log --ignore-working-copy --no-graph --color never -r @ -T '
      separate(
          " ",
          bookmarks.join(", "),
          coalesce(
              surround(
                  "\"",
                  "\"",
                  if(
                      description.first_line().substr(0, 24).starts_with(description.first_line()),
                      description.first_line().substr(0, 24),
                      description.first_line().substr(0, 23) ++ "…"
                  )
              ),
              label(if(empty, "empty"), description_placeholder)
          ),
          change_id.shortest(),
          commit_id.shortest(),
          if(conflict, label("conflict", "(conflict)")),
          if(empty, label("empty", "(empty)")),
          if(divergent, "(divergent)"),
          if(hidden, "(hidden)"),
      )
  '
}

main() {
  autoload -U colors && colors

  if jj_repo; then
    echo -ne "$(jj_status)" && return
  fi

	if in_repo; then
		echo -ne "%{%F{247}%}$(branch_raw)%{%F{252}%}@%{%F{247}%}$(commit) $(remote_commits_ahead):$(remote_commits_behind)$(diff)%f%k"
	fi
}

main
