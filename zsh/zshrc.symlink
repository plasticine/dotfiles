__dotfiles::welcome() {
  print -P "%F{white}
%B%D{%A %f %B %Y, %H:%M:%S %p}%b

    uptime %B$(uptime | sed -e "s/^.* up/up/g" | sed -e 's/,.*//g')%b
      load %B$(uptime | sed -e 's/^.*load averages: //g')%b
     procs %B$(ps ax | wc -l | tr -d ' ')%b
%f"
}

__dotfiles::load() {
  for file in "$@"; do source $file; done
}

__dotfiles::main() {
  # shortcut to this dotfiles path is $DOTFILES
  export DOTFILES=$HOME/.dotfiles

  # All of our *.zsh files
  typeset -U config_files
  config_files=($DOTFILES/**/*.zsh)

  # Load path files.
  __dotfiles::load ${(M)config_files:#*/path.zsh}

  # Load all files that aren't completion or path files
  __dotfiles::load ${${config_files:#*/path.zsh}:#*/completion.zsh}

  # Initialize autocomplete here, otherwise functions won't be loaded.
  autoload -Uz compinit && compinit

  # Load completion files
  __dotfiles::load ${(M)config_files:#*/completion.zsh}

  unset config_files

  # use .localrc for SUPER SECRET CRAP that you don't
  # want in your public, versioned repo.
  [[ -a ~/.localrc ]] && source ~/.localrc

  __dotfiles::welcome
  times
}

__dotfiles::main
